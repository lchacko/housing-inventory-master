set terminal postscript

set logscale y

# 1: year
# 2: median rent
# 3: housing units extant
# 4: net new housing units
# 5: employment in city
# 6: total wages of these employees
# 7: consumer price index

set xlabel "Year"
set ylabel "Median advertised asking rent"
set xrange [1946:2018]
plot "combined" using 1:2 title "Advertised monthly rent", 78 * 1.066 ** (x - 1956)

set ylabel "CPI-adjusted (1983-1984 dollars) median advertised rent"
plot "combined" using 1:($2 / $7) title "CPI-adjusted advertised rent", 316 * 1.025 ** (x - 1956)



unset logscale y
set xrange [1906:2015]
set yrange [0:]
set ylabel "Housing units"
plot "combined" using 1:4 with steps title "Net new housing units constructed"

set ylabel "Housing units"
plot "combined" using 1:3 with steps title "Total housing units existing"

unset yrange


set logscale y
set ylabel "CPI-adjusted median advertised asking rent and wages"
set xrange [1973:2016]
plot "combined" using 1:($2 / $7) title "Median asking monthly rent", "combined" using 1:($6 / $7 / 1000000 / 12) title "Total CPI-adjusted monthly wages paid, divided by 1 million"

set logscale y
set ylabel "CPI-adjusted median wages"
set xrange [1973:2016]
plot "combined" using 1:($11 / $7 / 12) title "Santa Clara County CPI-adjusted monthly wages paid", "combined" using 1:($6 / $7 / 12) title "San Francisco CPI-adjusted monthly wages paid"

set dummy per_capita_wages, employment

f(per_capita_wages, employment) = m * per_capita_wages ** pcw_e * employment ** employment_e

m               = 2.03118e-11
pcw_e           = 1.35374
employment_e    = 1.31782

unset xrange
FIT_LIMIT=1e-6
fit log(f(per_capita_wages, employment)) "combined" using ($6 / $7 / $5):5:(log($2 / $7)) via m, pcw_e, employment_e

set logscale y
set ylabel "CPI-adjusted median advertised asking rent"
set xrange [1973:2016]
plot "combined" using 1:($2 / $7) title "Median asking monthly rent", "combined" using 1:(f($6 / $7 / $5, $5)) with lines title "2.03118e-11 * wages ** 1.35374 * employment ** 1.31782"

set dummy units, per_capita_wages, employment

f(units, per_capita_wages, employment) = m * units ** units_e * per_capita_wages ** pcw_e * employment ** employment_e

m               = 0.377216
units_e         = -2.52214
pcw_e           = 1.89709
employment_e    = 1.54156

unset xrange
FIT_LIMIT=1e-6
fit log(f(units, per_capita_wages, employment)) "combined" using 3:($6 / $7 / $5):5:(log($2 / $7)) via m, units_e, pcw_e, employment_e

set logscale y
set ylabel "CPI-adjusted median advertised asking rent"
set xrange [1973:2016]
plot "combined" using 1:($2 / $7) title "Median asking monthly rent", "combined" using 1:(f($3, $6 / $7 / $5, $5)) with lines title "0.377216 * units ** -2.52214 * wages ** 1.89709 * employment ** 1.54156"

set ylabel "Unexplained variation in rent"
set yrange [2.0/3:1.5]
set ytics (2.0/3, 1/1.25, 1, 1.25, 1.5)
plot "combined" using 1:($2 / $7 / f($3, $6 / $7 / $5, $5)) with lines
